plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
}

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.59.0'
    protobufVersion = '3.25.1'
    javaxAnnotationVersion = '1.3.2'
    junitVersion = '4.13.2'
}

dependencies {
    implementation platform("io.grpc:grpc-bom:${grpcVersion}")

    implementation 'io.grpc:grpc-stub'
    implementation 'io.grpc:grpc-protobuf'
    runtimeOnly 'io.grpc:grpc-netty-shaded'

    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    compileOnly "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}"

    testImplementation "io.grpc:grpc-testing"
    testImplementation "junit:junit:${junitVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}


application {
    mainClass = 'pl.agh.edu.sr.smarthome.server.SmartHomeServer'
}


java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}